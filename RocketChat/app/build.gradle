buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
plugins {
    id "me.tatarka.retrolambda" version "3.2.4"
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.neenbedankt.android-apt'

repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url 'https://maven.fabric.io/public' }
    maven {
        url "https://jitpack.io"
    }

}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "chat.rocket.app"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        def twitterFile = file('twitter.properties')
        if (twitterFile.exists()) {
            def Properties twitterProperties = new Properties()
            twitterProperties.load(new FileInputStream(twitterFile));
            buildConfigField "String", "TWITTER_KEY", twitterProperties['TWITTER_KEY']
            buildConfigField "String", "TWITTER_SECRET", twitterProperties['TWITTER_SECRET']
        }else{
            buildConfigField "String", "TWITTER_KEY", '""'
            buildConfigField "String", "TWITTER_SECRET", '""'
        }
    }
    signingConfigs {
        debug {
            storeFile file('../keys/debug.keystore')
            keyAlias 'androiddebugkey'
            keyPassword 'android'
            storePassword 'android'
        }
    }

    packagingOptions {
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/NOTICE'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "WS_URL", "\"wss://demo.rocket.chat/websocket\""

        }
        debug {
            minifyEnabled false
            //buildConfigField "String", "WS_URL", "\"ws://192.168.0.14:3000/websocket\""
            buildConfigField "String", "WS_URL", "\"wss://demo.rocket.chat/websocket\""

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'



    apt 'com.google.dagger:dagger-compiler:2.0.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.5.3@aar') {
        transitive = true;
    }
    compile('com.twitter.sdk.android:twitter:1.10.0@aar') {
        transitive = true;
    }
    compile('com.github.ozodrukh:CircularReveal:1.1.1@aar') {
        transitive = true;
    }
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.code.gson:gson:2.5'
    compile 'com.squareup.okhttp:okhttp-ws:2.6.0'
    compile 'com.squareup.okhttp:logging-interceptor:2.6.0'
    compile 'com.facebook.android:facebook-android-sdk:4.8.1'
    compile 'com.google.dagger:dagger:2.0.1'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'io.reactivex:rxjava:1.0.16'
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.trello:rxlifecycle-components:0.3.0'
    compile 'com.google.android.gms:play-services-gcm:8.3.0'
}
